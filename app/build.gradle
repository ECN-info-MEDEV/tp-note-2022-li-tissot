import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    // Apply the application plugin to add support for building a CLI application in Java.
    id 'application'
    // Lombok
    id "io.freefair.lombok" version "6.3.0"
    // sonar scanner
    id "org.sonarqube" version "3.3"
    id "jacoco"
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

ext {
   javaMainClass = "ecn.App"
}

dependencies {
    // Use JUnit Jupiter for testing.
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'

    // This dependency is used by the application.
    implementation 'com.google.guava:guava:30.1.1-jre'
}

application {
    // Define the main class for the application.
    mainClass.set(javaMainClass)
}

jar {
  manifest {
    attributes(
      // Define the main class for the jar
      'Main-Class': javaMainClass
    )
  }
}

tasks.named('test') {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
}

// Tasks

tasks.javadoc.doFirst {
    println 'Exporting javadoc under `./build/docs/javadoc`...'
}

tasks.build.doFirst {
    println 'Building...'
}

tasks.build.dependsOn('jar')
tasks.build.dependsOn('testOpen')
tasks.build.dependsOn('sonarOpen')
tasks.sonarqube.dependsOn('jacocoTestReport')

tasks.sonarqube.doFirst {
    println 'Scanning with sonarqube...'
}

tasks.run.doFirst {
    println 'Running...'
}

tasks.test.doFirst {
    println 'Testing...'
}

tasks.jar.doFirst {
    println 'Generating jar under `./build/libs`'
}

task javadocOpen(type:Exec) {
    dependsOn 'javadoc'
    description 'Opens javadoc.'

    workingDir './build/docs/javadoc'

    // Let Linux be the default case.
    commandLine 'xdg-open', 'index.html'
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        workingDir System.getProperty("user.dir") + '/build/docs/javadoc'
        commandLine 'pwsh', '-Command', 'Invoke-Item index.html'
    } else if (Os.isFamily(Os.FAMILY_MAC)) {
        commandLine 'open', 'index.html'
    }
}

task testOpen(type:Exec) {
    dependsOn 'test'
    description 'run tests and open report.'

    workingDir './build/reports/tests/test'

    // Let Linux be the default case.
    commandLine 'xdg-open', 'index.html'
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        workingDir System.getProperty("user.dir") + '/build/reports/tests/test'
        commandLine 'pwsh', '-Command', 'Invoke-Item index.html'
    } else if (Os.isFamily(Os.FAMILY_MAC)) {
        commandLine 'open', 'index.html'
    }
}

task sonarOpen(type:Exec) {
    dependsOn 'sonarqube'
    description 'Generates and open sonarqube report.'

    // Let Linux be the default case.
    commandLine 'xdg-open', 'http://localhost:9000/dashboard?id=ds-li-tissot%3Aapp'
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine 'pwsh', '-Command', 'Invoke-Item http://localhost:9000/dashboard?id=ds-li-tissot%3Aapp'
    } else if (Os.isFamily(Os.FAMILY_MAC)) {
        commandLine 'open', 'http://localhost:9000/dashboard?id=ds-li-tissot%3Aapp'
    }
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    additionalSourceDirs.from = files(sourceSets.main.allJava.srcDirs)
    reports {
        html.enabled true
        xml.enabled true
        csv.enabled false
        html.destination file("./build/reports/jacoco/html")
    }
    executionData.from = files('./build/jacoco/test.exec')    
}

task sonarUp(type:Exec) {
    workingDir '..'
    commandLine 'docker-compose', 'up', '-d'
}

task sonarDown(type:Exec) {
    workingDir '..'
    commandLine 'docker-compose', 'down'
}

task sonarToken(type:Exec) {
    workingDir '..'
    commandLine 'curl', '-X POST', '-u admin:abc', '-F "name=' + UUID.randomUUID().toString() + '"', 'http://localhost:9000/api/user_tokens/generate'
}
